# Maintainer: archypie-project <archypie-project@protonmail.com>

pkgname=sdl2-arpie
pkgver=2.32.10
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, & keyboard (Version 2)"
arch=('x86_64' 'i686' 'aarch64' 'armv7h')
url="https://www.libsdl.org"
license=('Zlib')
depends=(
    'glibc'
    'hidapi'
    'libgl'
    'libusb'
    'libx11'
    'libxcursor'
    'libxext'
    'libxrender'
)
makedepends=(
    'alsa-lib'
    'clang'
    'cmake'
    'fcitx5'
    'ibus'
    'jack'
    'libdecor'
    'libpulse'
    'libxinerama'
    'libxkbcommon'
    'libxrandr'
    'libxss'
    'mesa'
    'mold'
    'ninja'
    'pipewire'
    'wayland-protocols'
    'wayland'
)
provides=('sdl2')
conflicts=('sdl2')
replaces=('sdl2')
source=(
    "https://github.com/libsdl-org/SDL/releases/download/release-${pkgver}/SDL2-${pkgver}.tar.gz"
    "01_rpi_kms_hints.patch"
)
sha512sums=(
    'cd4c040ebe4ec74250e32b401a292658353721dda30ad1066522b2a9de9a07560313978880a0bad7e7f5103cc14278fcbe27dbc5a188189e7fb6f097e7308550'
    'SKIP'
)
options=('!debug')

prepare() {
    cd "${srcdir}/SDL2-${pkgver}"

    patch -Np1 -i ../01_rpi_kms_hints.patch
}

build() {
    cd "${srcdir}/SDL2-${pkgver}"

    CFLAGS+=" -ffat-lto-objects"

    cmake . \
        -B"build" \
        -G"Ninja" \
        -DCMAKE_BUILD_RPATH_USE_ORIGIN="ON" \
        -DCMAKE_BUILD_TYPE="Release" \
        -DCMAKE_C_COMPILER="clang" \
        -DCMAKE_CXX_COMPILER="clang++" \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_LINKER_TYPE="MOLD" \
        -DSDL_KMSDRM_SHARED="ON" \
        -DSDL_KMSDRM="ON" \
        -DSDL_RPATH="OFF" \
        -DSDL_RPI="OFF" \
        -DSDL_STATIC="OFF" \
        -Wno-dev
    ninja -C build
}

package() {
    cd "${srcdir}/SDL2-${pkgver}"

    DESTDIR="${pkgdir}" ninja -C build install

    install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

